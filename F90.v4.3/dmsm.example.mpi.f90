!    Copyright (c) 2009-2012 by High Performance Computing Virtual Laboratory
!
!    This is a square root summation example,
!    performed by subgroups of processes in MPI
!    parallelism, by employing special case 3
!    of the DMSM library.
!
!    In this test, there are three input data
!    files. One is
!    MPI.PROCESSES.IN.JOB.COMM.dat
!    which containes only one integer as it names.
!
!    Another input data file is in.dat, which
!    containes two lines of integers as follows.
!
!    The first line is the JOB_DISTRIBUTION_PLAN
!    which can only take 11, 12, 13, 21, 22, 23,
!    31, 32, or 33.
!
!    The second line is the total number of jobs,
!    and number of jobs in each job group.
!
!    The last file is integers.dat, which is
!    the upper bound for square root summation.
!
!    The comment lines of the file dmsm.f90 is
!    a little more detailed description of the
!    DMSM model and its examples.


MODULE  MY_DATA
  INCLUDE                 'mpif.h'
  INTEGER              :: MY_MPI_RANK, TOTAL_MPI_PROCESSES, IERR
  INTEGER              :: JOB_COMM,  JOB_RANK,  JOB_PROCESSES
  INTEGER              :: DIST_COMM, DIST_RANK, DIST_PROCESSES
  INTEGER              :: JOB_PROCESSES_EXPECTED
  INTEGER              :: JOB_DISTRIBUTION_PLAN
  INTEGER              :: TOTAL_JOBS, NUM_OF_JOBS_PER_GROUP
  INTEGER, ALLOCATABLE :: INITIAL_DATA_FOR_JOBS(:)
  INTEGER, ALLOCATABLE :: INITIAL_DATA_FOR_A_JOB_GROUP(:)
  INTEGER              :: FINAL_ELEMENTS_MASTER=0
  DOUBLE PRECISION, ALLOCATABLE :: FINAL_RESULT_IN_MASTER(:)
  INTEGER, ALLOCATABLE :: FINAL_RESULT_IN_MASTER_IN(:,:)
  INTEGER              :: FINAL_ELEMENTS_PROCESS=0
  DOUBLE PRECISION, ALLOCATABLE :: FINAL_RESULT_IN_PROCESS(:)
  INTEGER, ALLOCATABLE :: FINAL_RESULT_IN_PROCESS_IN(:,:)
END MODULE  MY_DATA


MODULE ALL_MODULES
  USE MY_DATA
  USE DMSM_MODULE
END MODULE  ALL_MODULES




PROGRAM A_DMSM_EXAMPLE
  USE   ALL_MODULES
  INTERFACE
      SUBROUTINE PREPARE_FOR_A_JOB_GROUP(JOB_START,JOB_END,MY_RANK,DESTINATION)
      INTEGER:: JOB_START,JOB_END,MY_RANK,DESTINATION
      END SUBROUTINE PREPARE_FOR_A_JOB_GROUP

      SUBROUTINE COLLECT_SOME_RESULTS(MY_RANK,FROM)
      INTEGER:: MY_RANK,FROM
      END SUBROUTINE COLLECT_SOME_RESULTS

      SUBROUTINE DO_MY_JOB(MY_JOB)
      INTEGER:: MY_JOB
      END SUBROUTINE DO_MY_JOB
  END INTERFACE

  CALL  MPIINIT
  CALL  DATA_INITIALIZE
  CALL  DMSM_GEN_COMM_MPI_ALL(JOB_PROCESSES_EXPECTED, &
                              MPI_COMM_WORLD, MY_MPI_RANK, TOTAL_MPI_PROCESSES, &
                              JOB_COMM,       JOB_RANK,    JOB_PROCESSES,       &
                              DIST_COMM,      DIST_RANK,   DIST_PROCESSES       )
  IF(MY_MPI_RANK.EQ.0)                     PRINT*, JOB_PROCESSES
  IF(MY_MPI_RANK.EQ.1)                     PRINT*, JOB_PROCESSES
  IF(MY_MPI_RANK.EQ.TOTAL_MPI_PROCESSES-1) PRINT*, JOB_PROCESSES
  CALL DMSM_MPI_ALL(TOTAL_JOBS, NUM_OF_JOBS_PER_GROUP,&
                    DO_MY_JOB, PREPARE_FOR_A_JOB_GROUP, COLLECT_SOME_RESULTS, .TRUE.)
  CALL  FINAL_OUTPUT()
  CALL  DATA_FINALIZE
  CALL  MPI_FINALIZE(IERR)
  STOP
END PROGRAM A_DMSM_EXAMPLE




SUBROUTINE MPIINIT
  USE  ALL_MODULES
  CALL MPI_INIT( IERR )
  CALL MPI_COMM_RANK(MPI_COMM_WORLD,MY_MPI_RANK,IERR)
  CALL MPI_COMM_SIZE(MPI_COMM_WORLD,TOTAL_MPI_PROCESSES,IERR)
  RETURN
END SUBROUTINE MPIINIT




SUBROUTINE DATA_INITIALIZE
  USE ALL_MODULES
  IMPLICIT  NONE
  INTEGER   :: I

  IF(MY_MPI_RANK.EQ.DMSM_GET_MASTER()) THEN
     OPEN(11,FILE="MPI.PROCESSES.IN.JOB.COMM.dat")
     READ(11,*) JOB_PROCESSES_EXPECTED
     CLOSE(11)
     OPEN(11,FILE="in.dat")
     READ(11,*) JOB_DISTRIBUTION_PLAN
     READ(11,*) TOTAL_JOBS, NUM_OF_JOBS_PER_GROUP
     CLOSE(11)
  END IF

  CALL MPI_BCAST(JOB_PROCESSES_EXPECTED,1,MPI_INTEGER,DMSM_GET_MASTER(),MPI_COMM_WORLD,IERR)
  CALL MPI_BCAST(JOB_DISTRIBUTION_PLAN, 1,MPI_INTEGER,DMSM_GET_MASTER(),MPI_COMM_WORLD,IERR)
  CALL MPI_BCAST(TOTAL_JOBS,            1,MPI_INTEGER,DMSM_GET_MASTER(),MPI_COMM_WORLD,IERR)
  CALL MPI_BCAST(NUM_OF_JOBS_PER_GROUP, 1,MPI_INTEGER,DMSM_GET_MASTER(),MPI_COMM_WORLD,IERR)

  IF(MY_MPI_RANK.EQ.DMSM_GET_MASTER()) THEN
     ALLOCATE(INITIAL_DATA_FOR_JOBS(TOTAL_JOBS))
	 OPEN(11,FILE="integers.dat")
        DO I=1,TOTAL_JOBS
           READ(11,*) INITIAL_DATA_FOR_JOBS(I)
        END DO
     CLOSE(11)
  END IF

  ALLOCATE(INITIAL_DATA_FOR_A_JOB_GROUP(NUM_OF_JOBS_PER_GROUP))

  ALLOCATE(FINAL_RESULT_IN_PROCESS(NUM_OF_JOBS_PER_GROUP))
  ALLOCATE(FINAL_RESULT_IN_PROCESS_IN(3,NUM_OF_JOBS_PER_GROUP))
  FINAL_ELEMENTS_PROCESS=0

  IF(MY_MPI_RANK.EQ.DMSM_GET_MASTER()) ALLOCATE(FINAL_RESULT_IN_MASTER(TOTAL_JOBS))
  IF(MY_MPI_RANK.EQ.DMSM_GET_MASTER()) ALLOCATE(FINAL_RESULT_IN_MASTER_IN(3,TOTAL_JOBS))
  FINAL_ELEMENTS_MASTER=0

  RETURN
END SUBROUTINE DATA_INITIALIZE




SUBROUTINE DATA_FINALIZE
  USE ALL_MODULES
  IMPLICIT  NONE
  IF(MY_MPI_RANK.EQ.DMSM_GET_MASTER()) THEN
     IF(ALLOCATED(INITIAL_DATA_FOR_JOBS))     DEALLOCATE(INITIAL_DATA_FOR_JOBS)
  END IF
  IF(ALLOCATED(INITIAL_DATA_FOR_A_JOB_GROUP)) DEALLOCATE(INITIAL_DATA_FOR_A_JOB_GROUP)
  IF(ALLOCATED(FINAL_RESULT_IN_MASTER))       DEALLOCATE(FINAL_RESULT_IN_MASTER)
  IF(ALLOCATED(FINAL_RESULT_IN_MASTER_IN))    DEALLOCATE(FINAL_RESULT_IN_MASTER_IN)
  IF(ALLOCATED(FINAL_RESULT_IN_PROCESS))      DEALLOCATE(FINAL_RESULT_IN_PROCESS)
  IF(ALLOCATED(FINAL_RESULT_IN_PROCESS_IN))   DEALLOCATE(FINAL_RESULT_IN_PROCESS_IN)
  RETURN
END SUBROUTINE DATA_FINALIZE




SUBROUTINE DO_MY_JOB(MY_JOB)
  USE ALL_MODULES
  IMPLICIT  NONE
  INTEGER:: MY_JOB,THEINTEGER,I
  DOUBLE PRECISION :: R, RT

  IF (JOB_RANK .EQ. 0) THEN
      THEINTEGER=INITIAL_DATA_FOR_A_JOB_GROUP(MY_JOB-DMSM_GET_GROUP_START(MY_JOB)+1)
  END IF
  CALL MPI_BCAST(THEINTEGER,1,MPI_INTEGER,0,JOB_COMM,IERR)
  RT=0.0D0
  DO I=JOB_RANK,THEINTEGER,JOB_PROCESSES
     RT=RT+SQRT(1.0D0*I)
  END DO

  R=0.0D0
  CALL MPI_REDUCE(RT,R,1,MPI_DOUBLE_PRECISION,MPI_SUM,0,JOB_COMM,IERR)
  IF (JOB_RANK .EQ. 0) THEN
      FINAL_ELEMENTS_PROCESS=FINAL_ELEMENTS_PROCESS+1
      FINAL_RESULT_IN_PROCESS_IN(1,FINAL_ELEMENTS_PROCESS)=MY_JOB
      FINAL_RESULT_IN_PROCESS_IN(2,FINAL_ELEMENTS_PROCESS)=THEINTEGER
      FINAL_RESULT_IN_PROCESS_IN(3,FINAL_ELEMENTS_PROCESS)=MY_MPI_RANK
      FINAL_RESULT_IN_PROCESS(FINAL_ELEMENTS_PROCESS)=R
  END IF

  RETURN
END SUBROUTINE DO_MY_JOB




SUBROUTINE PREPARE_FOR_A_JOB_GROUP(JOB_START,JOB_END,MY_RANK,DESTINATION)
   USE ALL_MODULES
   IMPLICIT  NONE
   INTEGER:: MY_RANK,DESTINATION
   INTEGER:: JOB_START, JOB_END, COUNTS, TAG=175
   INTEGER:: MPISTTS(MPI_STATUS_SIZE)

   IF((MY_RANK.NE.DMSM_GET_MASTER()).AND.(MY_RANK.NE.DESTINATION)) THEN
      WRITE(*,*) "ERROR RANKS: ",MY_RANK, ", ", DMSM_GET_MASTER()," AND ",DESTINATION
      STOP
   END IF

   COUNTS=JOB_END-JOB_START+1

   IF(DESTINATION.EQ.DMSM_GET_MASTER()) THEN
      INITIAL_DATA_FOR_A_JOB_GROUP(1:COUNTS)=INITIAL_DATA_FOR_JOBS(JOB_START:JOB_END)
   ELSE
      IF(MY_RANK.EQ.DMSM_GET_MASTER()) THEN
         CALL MPI_SEND(INITIAL_DATA_FOR_JOBS(JOB_START:),COUNTS,MPI_INTEGER, &
                       DESTINATION,      TAG,DIST_COMM,IERR)
      ELSE
         CALL MPI_RECV(INITIAL_DATA_FOR_A_JOB_GROUP,     COUNTS,MPI_INTEGER, &
                       DMSM_GET_MASTER(),TAG,DIST_COMM,MPISTTS,IERR)
      END IF
   END IF

   RETURN
END SUBROUTINE PREPARE_FOR_A_JOB_GROUP




SUBROUTINE COLLECT_SOME_RESULTS(MY_RANK,FROM)
  USE ALL_MODULES
  IMPLICIT  NONE
  INTEGER:: MY_RANK,FROM
  INTEGER:: L,S,TE,TAG
  INTEGER:: MPISTTS(MPI_STATUS_SIZE)

  IF((MY_RANK.NE.DMSM_GET_MASTER()).AND.(MY_RANK.NE.FROM)) THEN
     WRITE(*,*) "ERROR RANKS: ",MY_RANK, ", ", DMSM_GET_MASTER()," AND ",FROM
     STOP
  END IF

  IF(DMSM_GET_MASTER().EQ.FROM) THEN
     IF(FINAL_ELEMENTS_PROCESS.GT.0) THEN
        FINAL_RESULT_IN_MASTER(FINAL_ELEMENTS_MASTER+1:FINAL_ELEMENTS_MASTER+FINAL_ELEMENTS_PROCESS)=&
                               FINAL_RESULT_IN_PROCESS(1:FINAL_ELEMENTS_PROCESS)
        FINAL_RESULT_IN_MASTER_IN(1:3,FINAL_ELEMENTS_MASTER+1:FINAL_ELEMENTS_MASTER+FINAL_ELEMENTS_PROCESS)=&
                               FINAL_RESULT_IN_PROCESS_IN(1:3,1:FINAL_ELEMENTS_PROCESS)
        FINAL_ELEMENTS_MASTER=FINAL_ELEMENTS_MASTER+FINAL_ELEMENTS_PROCESS
        FINAL_ELEMENTS_PROCESS=0
     END IF

  ELSE
     IF(MY_RANK.EQ.DMSM_GET_MASTER()) THEN
        CALL MPI_RECV(TE,1,MPI_INTEGER,FROM,MPI_ANY_TAG,DIST_COMM,MPISTTS,IERR)
        IF(TE.GT.0) THEN
           TAG=MPISTTS(MPI_TAG)
           CALL MPI_RECV(FINAL_RESULT_IN_MASTER(FINAL_ELEMENTS_MASTER+1:FINAL_ELEMENTS_MASTER+TE), &
                         TE,MPI_DOUBLE_PRECISION,FROM,TAG,DIST_COMM,MPISTTS,IERR)
           CALL MPI_RECV(FINAL_RESULT_IN_MASTER_IN(1:3,FINAL_ELEMENTS_MASTER+1:FINAL_ELEMENTS_MASTER+TE), &
                         3*TE,MPI_INTEGER,FROM,TAG,DIST_COMM,MPISTTS,IERR)
           FINAL_ELEMENTS_MASTER=FINAL_ELEMENTS_MASTER+TE
        END IF

     ELSE
        CALL MPI_SEND(FINAL_ELEMENTS_PROCESS,1,MPI_INTEGER,DMSM_GET_MASTER(),DMSM_GET_TAG(),DIST_COMM,IERR)
        IF(FINAL_ELEMENTS_PROCESS.GT.0) THEN
           CALL MPI_SEND(FINAL_RESULT_IN_PROCESS(1:FINAL_ELEMENTS_PROCESS),FINAL_ELEMENTS_PROCESS,&
                                   MPI_DOUBLE_PRECISION,DMSM_GET_MASTER(),DMSM_GET_TAG(),DIST_COMM,IERR)
           CALL MPI_SEND(FINAL_RESULT_IN_PROCESS_IN(1:3,1:FINAL_ELEMENTS_PROCESS),3*FINAL_ELEMENTS_PROCESS,&
                                            MPI_INTEGER,DMSM_GET_MASTER(),DMSM_GET_TAG(),DIST_COMM,IERR)
           FINAL_ELEMENTS_PROCESS=0
        END IF

     END IF

  END IF

  RETURN
END SUBROUTINE COLLECT_SOME_RESULTS




SUBROUTINE FINAL_OUTPUT()
  USE ALL_MODULES
  IMPLICIT  NONE
  INTEGER:: I,J,K(3)
  DOUBLE PRECISION :: R
  IF (MY_MPI_RANK .EQ. DMSM_GET_MASTER())   THEN
      !OPEN(14,FILE="sqrtrootsumallo.dat")
      !    WRITE(14,*) FINAL_ELEMENTS_MASTER, TOTAL_JOBS
      !    DO I=1, FINAL_ELEMENTS_MASTER
      !       WRITE(14,*) FINAL_RESULT_IN_MASTER_IN(1:3,I)
      !       WRITE(14,*) FINAL_RESULT_IN_MASTER(I)
      !    END DO
      !    WRITE(14,*) "DONE BY EXAMPLE4."
      !CLOSE(14)
      OPEN(14,FILE="sqrtrootsumall.dat")
          WRITE(14,*) FINAL_ELEMENTS_MASTER, TOTAL_JOBS
          DO I=1, FINAL_ELEMENTS_MASTER-1
          DO J=I+1, FINAL_ELEMENTS_MASTER
             IF(FINAL_RESULT_IN_MASTER_IN(1,I).GT.FINAL_RESULT_IN_MASTER_IN(1,J)) THEN
                K=FINAL_RESULT_IN_MASTER_IN(1:3,I)
                FINAL_RESULT_IN_MASTER_IN(1:3,I)=FINAL_RESULT_IN_MASTER_IN(1:3,J)
                FINAL_RESULT_IN_MASTER_IN(1:3,J)=K
                R=FINAL_RESULT_IN_MASTER(I)
                FINAL_RESULT_IN_MASTER(I)=FINAL_RESULT_IN_MASTER(J)
                FINAL_RESULT_IN_MASTER(J)=R
             END IF
          END DO
          END DO
          DO I=1, FINAL_ELEMENTS_MASTER
             WRITE(14,*) I, FINAL_RESULT_IN_MASTER(I)
          END DO
          WRITE(14,*) "DONE BY dmsm.example.mpi.f90."
      CLOSE(14)
      WRITE(*,*) "DONE BY dmsm.example.mpi.f90."
  END IF
  RETURN
END SUBROUTINE FINAL_OUTPUT




